model {
    publishing {
        publications {
            for (publication in project.ext.publications) {
                "$publication.name"(MavenPublication) {
                    groupId project.group
                    artifactId publication.artifactId
                    version project.version
                    artifact(publication.artifact) {
                        classifier publication.classifier
                    }

                    pom.withXml {
                        def root = asNode()
                        def dependencies = root.appendNode("dependencies")
                        configurations."$publication.dependencyConfiguration".allDependencies.each {
                            if (it['group'] != null) {
                                def dependency = dependencies.appendNode("dependency")
                                dependency?.appendNode("groupId", it["group"])
                                dependency?.appendNode("artifactId", it["name"])
                                dependency?.appendNode("version", it["version"])
                            }
                        }
                    }
                }
            }
        }
        repositories {
            if (project.version.endsWith('-SNAPSHOT')) {
                maven { url 'http://maven/libs-snapshot' }
                maven { url 'http://maven/artifactory/libs-snapshot' }
            } else {
                maven { url 'http://maven/libs-release' }
                maven { url 'http://maven/artifactory/libs-release' }
            }
        }
    }
}